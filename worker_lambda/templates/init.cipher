CALL apoc.cypher.runMany(
'
{# 既存のノードを全て削除 #}
MATCH (n) DETACH DELETE n;

{# 制約の設定 #}
CREATE CONSTRAINT c_database_role_name_is_unique IF NOT EXISTS FOR (node:DatabaseRole) REQUIRE node.name IS UNIQUE;
CREATE CONSTRAINT c_functional_role_name_is_unique IF NOT EXISTS FOR (node:FunctionalRole) REQUIRE node.name IS UNIQUE;
CREATE CONSTRAINT c_access_role_name_is_unique IF NOT EXISTS FOR (node:AccessRole) REQUIRE node.name IS UNIQUE;
CREATE CONSTRAINT c_database_module_id_is_unique IF NOT EXISTS FOR (node:Database) REQUIRE node.module_id IS UNIQUE;
CREATE CONSTRAINT c_schema_module_id_is_unique IF NOT EXISTS FOR (node:Schema) REQUIRE node.module_id IS UNIQUE;


{# データベースの代入 #}
UNWIND $database_node_params AS nodeParam
CREATE (n:Database:DatabaseObject)
SET n = nodeParam;

{# スキーマの代入 #}
UNWIND $schema_node_params AS nodeParam
CREATE (n:Schema:DatabaseObject)
SET n = nodeParam;

{# データベースとスキーマのリレーションの代入 #}
UNWIND $schema_node_params AS nodeParam
MERGE (database:Database {module_id: nodeParam.database_id})
MERGE (schema:Schema {module_id: nodeParam.module_id})
MERGE (database)-[rel:CONTAINS]->(schema);

{# データベースロールの代入 #}
UNWIND $database_read_role_node_params AS nodeParam
CREATE (n:DatabaseRole:ReadRole)
SET n = nodeParam;

UNWIND $database_read_write_role_node_params AS nodeParam
CREATE (n:DatabaseRole:ReadWriteRole)
SET n = nodeParam;

{# データベースロールとデータベースのリレーションの代入 #}
UNWIND $database_read_role_node_params AS nodeParam
UNWIND nodeParam.schema_ids AS schema_id
MERGE (database_role:DatabaseRole {module_id: nodeParam.module_id})
MERGE (database:Database {module_id: nodeParam.database_id})
MERGE (schema:Schema {module_id: schema_id})
MERGE (database_role)-[rel1:TARGETS_DATABASE]->(database)
MERGE (database_role)-[rel2:TARGETS_SCHEMA]->(schema);

UNWIND $database_read_write_role_node_params AS nodeParam
UNWIND nodeParam.schema_ids AS schema_id
MERGE (database_role:DatabaseRole {module_id: nodeParam.module_id})
MERGE (database:Database {module_id: nodeParam.database_id})
MERGE (schema:Schema {module_id: schema_id})
MERGE (database_role)-[rel1:TARGETS_DATABASE]->(database)
MERGE (database_role)-[rel2:TARGETS_SCHEMA]->(schema);

{# 権限の定義 #}
{# RW_ROLE on DATABASE #}
UNWIND $env_list AS env
MATCH (wrRole:ReadWriteRole {env: env}) -[rel1:TARGETS_DATABASE]->(database: DatabaseObject {env: env})
UNWIND $priv_list_rw_role_on_database AS privType
MERGE (p:Privilege {target: database.name, priv_type: privType, name: privType, env: env})
MERGE (wrRole)-[:HAS_PRIVILEGE]->(p)
MERGE (p)-[:GRANTED_ON]->(database);

{# R_ROLE on DATABASE #}
UNWIND $env_list AS env
MATCH (rRole:ReadRole {env: env}) -[rel1:TARGETS_DATABASE]->(database: DatabaseObject {env: env})
UNWIND $priv_list_r_role_on_database AS privType
MERGE (p:Privilege {target: database.name, priv_type: privType, name: privType, env: env})
MERGE (rRole)-[:HAS_PRIVILEGE]->(p)
MERGE (p)-[:GRANTED_ON]->(database);

{# RW_ROLE on SCHEMA #}
UNWIND $env_list AS env
MATCH (wrRole:ReadWriteRole {env: env}) -[rel2:TARGETS_SCHEMA]->(schema: DatabaseObject {env: env})
UNWIND $priv_list_rw_role_on_schema AS privType
MERGE (p:Privilege {target: schema.name, priv_type: privType, name: privType, env: env})
MERGE (wrRole)-[:HAS_PRIVILEGE]->(p)
MERGE (p)-[:GRANTED_ON]->(schema);

{# R_ROLE on SCHEMA #}
UNWIND $env_list AS env
MATCH (rRole:ReadRole {env: env}) -[rel2:TARGETS_SCHEMA]->(schema: DatabaseObject {env: env})
UNWIND $priv_list_r_role_on_schema AS privType
MERGE (p:Privilege {target: schema.name, priv_type: privType, name: privType, env: env})
MERGE (rRole)-[:HAS_PRIVILEGE]->(p)
MERGE (p)-[:GRANTED_ON]->(schema);

{# 不要になったリレーションの削除#}
MATCH (role:DatabaseRole)-[r:TARGETS_DATABASE]->(obj:Database)
DELETE r;
MATCH (role:DatabaseRole)-[r:TARGETS_SCHEMA]->(obj:Schema)
DELETE r;

{# 機能ロールの代入 #}
UNWIND $functional_role_node_params AS nodeParam
CREATE (n:FunctionalRole)
SET n = nodeParam;

{# アクセスロールの代入 #}
UNWIND $access_role_node_params AS nodeParam
CREATE (n:AccessRole)
SET n = nodeParam;

{# データベースロールの継承の代入 #}
UNWIND range(0, size($database_role_inheritance_node_params) - 1) AS i
MERGE (child:DatabaseRole {name: $database_role_inheritance_node_params[i].child})
MERGE (parent:AccessRole {name: $database_role_inheritance_node_params[i].parent})
MERGE (parent)-[rel:INHERITS_FROM]->(child);

{# 機能ロールの継承の代入 #}
UNWIND range(0, size($functional_role_inheritance_node_params) - 1) AS i
MERGE (child:FunctionalRole {name: $functional_role_inheritance_node_params[i].child})
MERGE (parent:FunctionalRole {name: $functional_role_inheritance_node_params[i].parent})
MERGE (parent)-[rel:INHERITS_FROM]->(child);

{# 機能アクセスロールの継承の代入 #}
UNWIND range(0, size($functional_access_role_inheritance_node_params) - 1) AS i
MERGE (child:AccessRole {name: $functional_access_role_inheritance_node_params[i].child})
MERGE (parent:FunctionalRole {name: $functional_access_role_inheritance_node_params[i].parent})
MERGE (parent)-[rel:INHERITS_FROM]->(child);

{# アクセスロールの継承の代入 #}
UNWIND range(0, size($access_role_inheritance_node_params) - 1) AS i
MERGE (child:AccessRole {name: $access_role_inheritance_node_params[i].child})
MERGE (parent:AccessRole {name: $access_role_inheritance_node_params[i].parent})
MERGE (parent)-[rel:INHERITS_FROM]->(child);

'
,{database_read_role_node_params: $database_read_role_node_params,
  database_read_write_role_node_params: $database_read_write_role_node_params,
  functional_role_node_params: $functional_role_node_params,
  access_role_node_params: $access_role_node_params,
  database_node_params: $database_node_params,
  schema_node_params: $schema_node_params,
  database_role_inheritance_node_params: $database_role_inheritance_node_params,
  functional_role_inheritance_node_params: $functional_role_inheritance_node_params,
  functional_access_role_inheritance_node_params: $functional_access_role_inheritance_node_params,
  access_role_inheritance_node_params: $access_role_inheritance_node_params,

  priv_list_rw_role_on_database: $priv_list_rw_role_on_database,
  priv_list_r_role_on_database: $priv_list_r_role_on_database,
  priv_list_rw_role_on_schema: $priv_list_rw_role_on_schema,
  priv_list_r_role_on_schema: $priv_list_r_role_on_schema,
  env_list: $env_list
}
)
